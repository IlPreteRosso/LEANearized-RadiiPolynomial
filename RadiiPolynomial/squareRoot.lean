import RadiiPolynomial.RadiiPolyGeneral
import Mathlib.Analysis.Calculus.FDeriv.Pow

open scoped Topology BigOperators
open Metric Set Filter ContinuousLinearMap

/-
Informal Goal: Verify existence of a unique root of f(x) = x¬≤-2 near xÃÑ = 1.3, with approximate inverse A = 0.38.

Formal Goal: Apply `simple_radii_polynomial_theorem_same_space` to conclude
‚ä¢ ‚àÉ! xÃÉ ‚àà closedBall (13/10) r‚ÇÄ, xÃÉ¬≤ - 2 = 0
-/

/-
# Example 2.4.5: Square Root Verification

Verify existence and uniqueness of a zero of f(x) = x¬≤ - 2 near xÃÑ = 1.3.

## Parameters (using exact rationals)
- f(x) = x¬≤ - 2
- xÃÑ = 1.3 = 13/10
- A = 0.38 = 19/50
- Y‚ÇÄ = 0.12 = 3/25
- Z‚ÇÄ = 0.012 = 3/250
- Z‚ÇÇ = 0.76 = 19/25
- r‚ÇÄ = 0.15 = 3/20

The radii polynomial p(r) = 0.76r¬≤ - 0.988r + 0.12 is negative on (0.136, 1.164).
-/


section smulCLM
/-!
## One-Dimensional Continuous Linear Maps

In the one-dimensional case ‚Ñù ‚ÜíL[‚Ñù] ‚Ñù, every continuous linear map is
scalar multiplication by some constant a ‚àà ‚Ñù.

`smul`Ôºå scalar multiplication, is rendered as `‚Ä¢` by LEAN in Unicode. We define `smulCLM a` as
the continuous linear map x ‚Ü¶ a¬∑x. First we establish key properties:
- Operator norm: ‚ÄñsmulCLM a‚Äñ = |a|
- Composition: (smulCLM a) ‚àò (smulCLM b) = smulCLM (a¬∑b)
- Subtraction: smulCLM a - smulCLM b = smulCLM (a - b)
- Injectivity: smulCLM a is injective iff a ‚â† 0

These lemmas enable working with the radii polynomial theorem in `‚Ñù`,
where the approximate inverse A and derivative Df(xÃÑ) are both scalar CLMs.
-/

/-- A scalar as a continuous linear map ‚Ñù ‚ÜíL[‚Ñù] ‚Ñù.

    For a ‚àà ‚Ñù, `smulCLM a` is the map x ‚Ü¶ a¬∑x.

    This is noncomputable because ‚Ñù arithmetic is noncomputable in Mathlib. -/
noncomputable abbrev smulCLM (a : ‚Ñù) : ‚Ñù ‚ÜíL[‚Ñù] ‚Ñù := a ‚Ä¢ ContinuousLinearMap.id ‚Ñù ‚Ñù

/-- Evaluation: (smulCLM a)(x) = a * x -/
@[simp]
lemma smulCLM_apply (a x : ‚Ñù) : smulCLM a x = a * x := by simp [smulCLM]

/-- Operator norm of scalar multiplication equals absolute value.

    ‚ÄñsmulCLM a‚Äñ_{op} = |a|

    This is the key fact enabling norm computations in 1D. -/
lemma norm_smulCLM (a : ‚Ñù) : ‚ÄñsmulCLM a‚Äñ = |a| := by
  rw [smulCLM, norm_smul, norm_id, mul_one, Real.norm_eq_abs]

/-- The identity map is smulCLM 1 -/
lemma id_eq_smulCLM_one : ContinuousLinearMap.id ‚Ñù ‚Ñù = smulCLM 1 := by
  ext; simp only [coe_id', id_eq, one_smul]

/-- Composition of scalar CLMs is multiplication of scalars.

    (smulCLM a) ‚àò (smulCLM b) = smulCLM (a * b)

    Used to compute A ‚àò Df(xÃÑ) when both are scalar multiplications. -/
lemma smulCLM_comp (a b : ‚Ñù) : (smulCLM a).comp (smulCLM b) = smulCLM (a * b) := by
  ext
  simp only [smulCLM_apply, comp_smul‚Çõ‚Çó, RingHom.id_apply, comp_id, coe_smul', coe_id',
    Pi.smul_apply, id_eq, smul_eq_mul, mul_one]
  field_simp

/-- Subtraction of scalar CLMs.

    smulCLM a - smulCLM b = smulCLM (a - b)

    Used to compute Df(c) - Df(xÃÑ) = smulCLM(2c) - smulCLM(2xÃÑ) = smulCLM(2(c - xÃÑ)). -/
lemma smulCLM_sub (a b : ‚Ñù) : smulCLM a - smulCLM b = smulCLM (a - b) := by
  ext;
  simp only [coe_sub', coe_smul', coe_id', Pi.sub_apply, Pi.smul_apply, id_eq, smul_eq_mul,
    mul_one, smulCLM_apply]

/-- Identity minus scalar CLM.

    I - smulCLM a = smulCLM (1 - a)

    Used to compute I - A‚àòDf(xÃÑ) for the Z‚ÇÄ bound. -/
lemma id_sub_smulCLM (a : ‚Ñù) :
    ContinuousLinearMap.id ‚Ñù ‚Ñù - smulCLM a = smulCLM (1 - a) := by
  rw [id_eq_smulCLM_one, smulCLM_sub]

/-- Injectivity of scalar multiplication.

    smulCLM a is injective iff a ‚â† 0.

    Required hypothesis for Proposition 2.3.1 (fixed point ‚Üî zero equivalence). -/
lemma smulCLM_injective {a : ‚Ñù} (ha : a ‚â† 0) : Function.Injective (smulCLM a) := by
  intro x y hxy
  simp only [smulCLM_apply] at hxy
  exact mul_left_cancel‚ÇÄ ha hxy

end smulCLM



section FDerivPolynomials
/-!
## Fr√©chet Derivatives

For f(x) = x¬≤ - 2, we have Df(x) = smulCLM (2x).
-/

/-- The Fr√©chet derivative of x¬≤ at x is multiplication by 2x.

    For f(y) = y¬≤, we have Df(x) : v ‚Ü¶ 2x¬∑v, i.e., Df(x) = smulCLM(2x).

    This follows from the power rule: D[y^n] = n¬∑y^(n-1)¬∑id. -/
lemma fderiv_sq (x : ‚Ñù) : fderiv ‚Ñù (fun y => y ^ 2) x = smulCLM (2 * x) := by
  have h := fderiv_pow (ùïú := ‚Ñù) (f := fun y => y) (x := x) (n := 2) differentiableAt_id
  simp only [fderiv_id'] at h
  ext
  simp only [h, smul_apply, id_apply, smul_eq_mul]
  ring

/-- The Fr√©chet derivative of x¬≤ - c -/
lemma fderiv_sq_sub_const (x : ‚Ñù) (c : ‚Ñù) :
    fderiv ‚Ñù (fun y => y ^ 2 - c) x = smulCLM (2 * x) := by
  rw [fderiv_sub_const, fderiv_sq]

lemma differentiable_sq_sub_const (c : ‚Ñù) : Differentiable ‚Ñù (fun y : ‚Ñù => y ^ 2 - c) :=
  (differentiable_id.pow 2).sub (differentiable_const c)

end FDerivPolynomials



section Example_2_4_5_Setup
/-!
## Example 2.4.5: Bounds Verification

All bounds use exact rational arithmetic for formal verification.
-/

/-! ### Parameter definitions -/

noncomputable abbrev ex_f : ‚Ñù ‚Üí ‚Ñù := fun x => x ^ 2 - 2
noncomputable abbrev ex_xBar : ‚Ñù := 13 / 10
noncomputable abbrev ex_A : ‚Ñù ‚ÜíL[‚Ñù] ‚Ñù := smulCLM (19 / 50)
noncomputable abbrev ex_Y‚ÇÄ : ‚Ñù := 3 / 25
noncomputable abbrev ex_Z‚ÇÄ : ‚Ñù := 3 / 250
noncomputable abbrev ex_Z‚ÇÇ : ‚Ñù ‚Üí ‚Ñù := fun _ => 19 / 25
noncomputable abbrev ex_r‚ÇÄ : ‚Ñù := 3 / 20

/-! ### Bound verification lemmas -/

/-- eq. 2.14: ‚ÄñA(f(xÃÑ))‚Äñ ‚â§ Y‚ÇÄ -/
lemma ex_bound_Y‚ÇÄ : ‚Äñex_A (ex_f ex_xBar)‚Äñ ‚â§ ex_Y‚ÇÄ := by
  simp only [ex_A, ex_f, ex_xBar, ex_Y‚ÇÄ, smulCLM_apply, Real.norm_eq_abs]
  -- (19/50) * ((13/10)¬≤ - 2) = (19/50) * (-31/100) = -589/5000
  -- |‚àí589/5000| ‚â§ 3/25 = 600/5000
  norm_num

/-- eq. 2.15: ‚ÄñI - A ‚àò Df(xÃÑ)‚Äñ ‚â§ Z‚ÇÄ -/
lemma ex_bound_Z‚ÇÄ : ‚ÄñContinuousLinearMap.id ‚Ñù ‚Ñù - ex_A.comp (fderiv ‚Ñù ex_f ex_xBar)‚Äñ ‚â§ ex_Z‚ÇÄ := by
  have hDf : fderiv ‚Ñù ex_f ex_xBar = smulCLM (2 * ex_xBar) := fderiv_sq_sub_const ex_xBar 2
  rw [hDf]
  rw [smulCLM_comp, id_sub_smulCLM, norm_smulCLM]
  simp only [ex_xBar, ex_Z‚ÇÄ]
  -- 1 - (19/50) * (2 * 13/10) = 1 - (19/50) * (13/5) = 1 - 247/250 = 3/250
  norm_num

/-- eq. 2.16: ‚ÄñA ‚àò [Df(c) - Df(xÃÑ)]‚Äñ ‚â§ Z‚ÇÇ(r) ¬∑ r for c ‚àà BÃÑ·µ£(xÃÑ) -/
lemma ex_bound_Z‚ÇÇ {c : ‚Ñù} (r : ‚Ñù)
  -- (hr : 0 ‚â§ r) (c : ‚Ñù)
  (hc : c ‚àà closedBall ex_xBar r) :
    ‚Äñex_A.comp (fderiv ‚Ñù ex_f c - fderiv ‚Ñù ex_f ex_xBar)‚Äñ ‚â§ ex_Z‚ÇÇ r * r := by
  have hDfc : fderiv ‚Ñù ex_f c = smulCLM (2 * c) := fderiv_sq_sub_const c 2
  have hDfx : fderiv ‚Ñù ex_f ex_xBar = smulCLM (2 * ex_xBar) := fderiv_sq_sub_const ex_xBar 2
  rw [hDfc, hDfx, smulCLM_sub, smulCLM_comp, norm_smulCLM]
  -- |(19/50) * (2c - 2xÃÑ)| = (19/50) * 2 * |c - xÃÑ| = (19/25) * |c - xÃÑ|
  have h1 : |(19 / 50 : ‚Ñù) * (2 * c - 2 * ex_xBar)| = (19 / 25) * |c - ex_xBar| := by
    rw [abs_mul]
    have : (2 : ‚Ñù) * c - 2 * ex_xBar = 2 * (c - ex_xBar) := by ring
    rw [this, abs_mul, abs_of_pos (by norm_num : (2 : ‚Ñù) > 0)]
    norm_num
    ring
  rw [h1]
  -- |c - xÃÑ| ‚â§ r from membership
  rw [mem_closedBall, Real.dist_eq] at hc
  simp only [ex_Z‚ÇÇ]
  exact mul_le_mul_of_nonneg_left hc (by norm_num)

/-- The radii polynomial is negative at r‚ÇÄ = 0.15 -/
lemma ex_radii_poly_neg : radiiPolynomial ex_Y‚ÇÄ ex_Z‚ÇÄ ex_Z‚ÇÇ ex_r‚ÇÄ < 0 := by
  unfold radiiPolynomial ex_Y‚ÇÄ ex_Z‚ÇÄ ex_Z‚ÇÇ ex_r‚ÇÄ
  norm_num

/-- r‚ÇÄ is positive -/
lemma ex_r‚ÇÄ_pos : 0 < ex_r‚ÇÄ := by simp [ex_r‚ÇÄ]

/-- A is injective -/
lemma ex_A_inj : Function.Injective ex_A := by
  exact smulCLM_injective (by norm_num : (19 / 50 : ‚Ñù) ‚â† 0)

/-- f is differentiable -/
lemma ex_f_diff : Differentiable ‚Ñù ex_f := differentiable_sq_sub_const 2

end Example_2_4_5_Setup



section Example_2_4_5
/-!
## Main Theorem Application

Apply simple_radii_polynomial_theorem_same_space from RadiiPolyGeneral.
-/

/-- Elements in the ball are positive (ball is [1.15, 1.45]) -/
lemma ball_elements_pos (y : ‚Ñù) (hy : y ‚àà closedBall ex_xBar ex_r‚ÇÄ) : 0 < y := by
  rw [mem_closedBall, Real.dist_eq] at hy
  simp only [ex_xBar, ex_r‚ÇÄ, abs_le] at hy
  -- |y - 13/10| ‚â§ 3/20 means 13/10 - 3/20 ‚â§ y, i.e., 23/20 ‚â§ y
  linarith

/-- Df(y) is invertible when y > 0 -/
lemma fderiv_invertible_of_pos {y : ‚Ñù} (hy : 0 < y) : (fderiv ‚Ñù ex_f y).IsInvertible := by
  have hDf : fderiv ‚Ñù ex_f y = smulCLM (2 * y) := fderiv_sq_sub_const y 2
  rw [hDf]
  -- smulCLM (2y) is invertible iff 2y ‚â† 0
  have h2y_ne : 2 * y ‚â† 0 := by linarith
  use ContinuousLinearEquiv.equivOfInverse (smulCLM (2 * y)) (smulCLM (1 / (2 * y)))
    (fun x => by simp only [smulCLM_apply]; field_simp [h2y_ne])
    (fun x => by simp only [smulCLM_apply]; field_simp [h2y_ne])
  rfl

/-- **Example 2.4.5**: There exists a unique xÃÉ ‚àà BÃÑ_{3/20}(13/10) with xÃÉ¬≤ = 2.

    Furthermore, Df(xÃÉ) is invertible, so xÃÉ is a nondegenerate zero. -/
theorem example_2_4_5 :
    ‚àÉ! xTilde ‚àà closedBall ex_xBar ex_r‚ÇÄ,
      ex_f xTilde = 0 ‚àß (fderiv ‚Ñù ex_f xTilde).IsInvertible := by
  exact simple_radii_polynomial_theorem_same_space
    ex_r‚ÇÄ_pos
    ex_bound_Y‚ÇÄ
    ex_bound_Z‚ÇÄ
    (fun c hc => ex_bound_Z‚ÇÇ ex_r‚ÇÄ hc)
    ex_f_diff
    ex_radii_poly_neg
    ex_A_inj

/-- Corollary: The zero is ‚àö2 (or -‚àö2, but our ball only contains ‚àö2) -/
theorem example_2_4_5_sqrt2 :
    ‚àÉ! xTilde ‚àà closedBall (ex_xBar : ‚Ñù) (ex_r‚ÇÄ), xTilde ^ 2 = 2 := by
  obtain ‚ü®xTilde, ‚ü®hMem, hZero, _‚ü©, hUniq‚ü© := example_2_4_5
  refine ‚ü®xTilde, ‚ü®hMem, ?_‚ü©, ?_‚ü©
  ¬∑ -- f(xTilde) = 0 means xTilde¬≤ - 2 = 0, so xTilde¬≤ = 2
    simp only [ex_f] at hZero
    linarith
  ¬∑ -- Uniqueness
    intro y ‚ü®hyMem, hySq‚ü©
    apply hUniq
    refine ‚ü®hyMem, ?_, ?_‚ü©
    ¬∑ simp only [ex_f]; linarith
    ¬∑ exact fderiv_invertible_of_pos (ball_elements_pos y hyMem)

end Example_2_4_5
